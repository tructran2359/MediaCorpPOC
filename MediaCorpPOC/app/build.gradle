apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.t.mediacorp2359pocs"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 4
        versionName "0.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->

            def formattedDate = new Date().format('yyyyMMdd')
            def appName = "Mediacorp2359POCs"
            def buildName = variant.buildType.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode

            outputFileName = "${formattedDate}_${appName}_${buildName}_v${versionName}_${versionCode}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += "$projectDir/src/main/proto"
    }
}


def protobufVersion = '3.12.0'
def protocVersion = protobufVersion

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha05'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation ('com.squareup.retrofit2:converter-protobuf:2.9.0') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }

    // Protobuff
    implementation "com.google.protobuf:protobuf-javalite:${protobufVersion}"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java{
                    option 'lite'
                }
            }
        }
    }
}